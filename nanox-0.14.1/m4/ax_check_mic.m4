# ===========================================================================
#   http://www.gnu.org/software/autoconf-archive/ax_check_compile_flag.html
# ===========================================================================
#
# SYNOPSIS
#
#   AX_CHECK_MIC
#
# DESCRIPTION
#
#   Checks that selected compilers are able to build binaries for Intel K1OM
#   architecture.
#   Checks are limited to check that the compiler used has the proper triplet set
#   or that an Intel compiler is being used.
#   In the case of the linker (LD), a GNU ld for this specific architecture is
#   required.
#   Allowed host triplets: x86_64-k1om-linux k1om-mpss-linux
#
# LICENSE
#
#   Copyright (c) 2015 Jorge Bellon <jbellon@bsc.es>
#
#   This program is free software: you can redistribute it and/or modify it
#   under the terms of the GNU General Public License as published by the
#   Free Software Foundation, either version 3 of the License, or (at your
#   option) any later version.
#
#   This program is distributed in the hope that it will be useful, but
#   WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General
#   Public License for more details.
#
#   You should have received a copy of the GNU General Public License along
#   with this program. If not, see <http://www.gnu.org/licenses/>.
#
#   As a special exception, the respective Autoconf Macro's copyright owner
#   gives unlimited permission to copy, distribute and modify the configure
#   scripts that are the output of Autoconf when processing the Macro. You
#   need not follow the terms of the GNU General Public License when using
#   or distributing such scripts, even though portions of the text of the
#   Macro appear in them. The GNU General Public License (GPL) does govern
#   all other use of the material that constitutes the Autoconf Macro.
#
#   This special exception to the GPL applies to versions of the Autoconf
#   Macro released by the Autoconf Archive. When you make and distribute a
#   modified version of the Autoconf Macro, you may extend this special
#   exception to the GPL to apply to your modified version as well.

AC_DEFUN([AX_CHECK_MIC],
[
AC_REQUIRE([AC_PROG_CXX])
AC_BEFORE([AX_COMPILE_VENDOR],[$0])dnl this macro uses info generated by this macro

  AS_IF([test "$cross_compiling" = no],
    [AC_MSG_ERROR([
-------------------------------
Cross compilation for Intel KNC detected, but
cross compile mode was not enabled by autoconf.
Check that a cross compiler is actually being
used @{:@i.e. use $host-gcc/g++@:}@ or
compile with Intel's icc/icpc using -mmic flag.
Recommended:
CC='icc -mmic' CXX='icpc -mmic'
LD=x86_64-k1om-linux-ld
-------------------------------])
    ])

  AS_CASE([$CC],
    [*x86_64-k1om-linux*],
      [supported=yes],
    [*k1om-mpss-linux*],
      [supported=yes],
    [
      AS_IF([test "$ax_cv_c_compiler_vendor" = intel],
        [supported=yes],
        [supported=no])
    ])

  AS_IF([test "$supported" = yes],[
    AS_CASE([$CXX],
      [*x86_64-k1om-linux*],
        [supported=yes],
      [*k1om-mpss-linux*],
        [supported=yes],
      [
        AS_IF([test "$ax_cv_cxx_compiler_vendor" = intel],
          [supported=yes],
          [supported=no])
      ])
  ])

  AS_IF([test "$supported" = yes],[
    AS_CASE([$LD],
      [*"x86_64-k1om-linux-ld "*],
        [supported=yes],
      [*"k1om-mpss-linux-ld "*],
        [supported=yes],
      [
        # Default
        supported=no
      ])
  ])

  AS_IF([test "$supported" = no],
    [AC_MSG_ERROR([
-------------------------------
Cross compilation for Intel KNC detected, but
$stage support check failed.
Please, select either a suitable cross-compiler
@{:@e.g.: $host-gcc@:}@ or Intel's icc.
Recommended:
CC='icc -mmic' CXX='icpc -mmic'
LD=x86_64-k1om-linux-ld
-------------------------------])
     ])
])dnl AX_CHECK_MIC

